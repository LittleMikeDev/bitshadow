defaultTasks 'build', 'shadowJar', 'mergedJacocoTestReport', 'publishToMavenLocal'

version = '0.1-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        jcenter()
    }
}

subprojects {
    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'org.assertj:assertj-core:3.1.0'
        testCompile 'org.mockito:mockito-core:1.10.8'
    }

    task testJar(type: Jar) {
        classifier "test"
        from sourceSets.test.output
    }
    artifacts {
        testRuntime testJar
    }
}

task mergedJacocoTestReport(type: JacocoReport) {
    dependsOn(subprojects.test)

    group = "Reporting"
    description = "Generate Jacoco coverage reports after running unit tests."
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)

    reports {
        html.enabled = true
        xml.enabled = true
    }

    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}
